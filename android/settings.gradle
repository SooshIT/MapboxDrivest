import org.gradle.authentication.http.BasicAuthentication

pluginManagement {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)

    def mapboxDownloadToken = providers.gradleProperty("MAPBOX_DOWNLOADS_TOKEN")
            .orElse(providers.environmentVariable("MAPBOX_DOWNLOADS_TOKEN"))
            .orNull ?: ""

    repositories {
        google()
        mavenCentral()
        maven {
            url = uri("https://api.mapbox.com/downloads/v2/releases/maven")
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = "mapbox"
                password = mapboxDownloadToken
            }
        }
    }
}

rootProject.name = "DrivestNavigationAndroid"
include ':app'
